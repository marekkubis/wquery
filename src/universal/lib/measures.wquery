#
# common functions
#
function min_size do
  emit distinct(min(size(%A)))
end

function max_size do
  emit distinct(max(size(%A)))
end

function subsumers do
  %l, %r := %A
  %lh := last(%l.hypernym*)
  %rh := last(%r.hypernym*)
  emit %lh intersect %rh
end

function lcs_by_depth do
  %m := maxby(subsumers(%A)$a<$a,min_size($a.hypernym*[empty(hypernym)])>, 2)
  emit as_synset(distinct(%m$a$_<$a>))
end

function lcs do
  %m := maxby(subsumers(%A)$a<$a,size($a.hypernym*[empty(hypernym)])>, 2)
  emit as_synset(distinct(%m$a$_<$a>))
end

#
# Path measure
#
function min_path_length do
  %l, %r := %A
  %lcs := lcs(%l, %r)

  %ll := min_size(%l.hypernym*.%lcs)
  %rl := min_size(%r.hypernym*.%lcs)
  emit %ll + %rl - 1
end

function path_measure do
  emit 1/min_path_length(%A)
end

#
# Leacock-Chodorow measure
#
function hierarchy_depth do
  %lcs := lcs(%A)
  %root := last(%lcs.hypernym*[empty(hypernym)])
  %depth := tree_depth(%root, `hypernym`)
  if [%root.pos = `v`]
    emit %depth + 1
  else
    emit %depth
end

function lch_measure do
  emit max(-log(min_path_length(%A)/(2*hierarchy_depth(%A))))
end

#
# Wu-Palmer measure
#
function root_dist do
  emit min_size(%A.hypernym*[empty(hypernym)])
end

function lcs_dist do
  %s, %lcs := %A
  emit min_size(%s.hypernym*.%lcs) - 1
end

function wup_measure do
  %l, %r := %A
  %lcs := lcs(%l, %r)
  %dl := lcs_dist(%l, %lcs)
  %dr := lcs_dist(%r, %lcs)
  %dlcs := root_dist(%lcs)
  emit 2*%dlcs/(%dl + %dr + 2*%dlcs)
end

#
# information content
#
function ic do
  %s := as_synset(%A)
  %c := tree_sum(%s, `hypernym`, `count`)
  %root := last(%s.hypernym*[empty(hypernym)])
  %d := tree_sum(%root, `hypernym`, `count`)
  %prob := %c/%d

  from %prob$p do
    if [$p > 0]
      emit -log($p)
    else
      emit $p
  end
end

function ic2 do
  %s := as_synset(%A)
  %c := tree_sum(%s, `hypernym`, `count`)
  %root := last(%s.hypernym*[empty(hypernym)])
  %d := tree_sum(%root, `hypernym`, `count`)
  emit -log(%c/%d)
end

function lcs_by_ic do
  %m := maxby(subsumers(%A)$a<$a,ic($a)>, 2)
  emit as_synset(distinct(%m$a$_<$a>))
end

#
# Resnik measure
#
function res_measure do
  %l, %r := %A
  %lcs := lcs_by_ic(%l, %r)

  emit ic(%lcs)
end

#
# Jiang-Conrath measure
#
function jcn_measure do
  %l, %r := %A
  %lcs := lcs_by_ic(%l, %r)
  %icl := ic2(%l)
  %icr := ic2(%r)

  if [%icl != 0 and %icr != 0]
    %dist := %icl + %icr - 2*ic2(%lcs)
  else
    %dist := 0

  if [%dist = 0] do
    %root := last(%lcs.hypernym*[empty(hypernym)])
    %d := distinct(max(tree_sum(%root, `hypernym`, `count`)))

    if [not empty(%d) and %d > 0.01]
       emit 1/-log((%d - 0.01)/%d)
     else
       emit 0
  end else do
    emit 1/%dist
  end
end

#
# Lin measure
#
function lin_measure do
  %l, %r := %A
  %lcs := lcs(%l, %r)
  %icl := ic(%l)
  %icr := ic(%r)

  if [%icl != 0 and %icr != 0]
    emit 2*ic(%lcs)/(%icl + %icr)
  else
    emit 0
end
